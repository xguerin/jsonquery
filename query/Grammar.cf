entrypoints Rule ;

-- Query

OrRule              . Rule            ::= Rule Or Rule2 ;
AndRule             . Rule2           ::= Rule2 And Rule3 ;
NotRule             . Rule4           ::= Not Rule3 ;
QrRule              . Rule5           ::= Query ;

coercions Rule 6;

VHas                . Query           ::= Has Path ;
VCompare            . Query           ::= Path VOp Value ;

AndOp               . And             ::= "and" ;
NegOp               . Not             ::= "not" ;
OrOp                . Or              ::= "or" ;
HasOp               . Has             ::= "has" ;

EqVOp               . VOp             ::= "==" ;
NeVOp               . VOp             ::= "<>" ;
GtVOp               . VOp             ::= ">" ;
GeVOp               . VOp             ::= ">=" ;
LtVOp               . VOp             ::= "<" ;
LeVOp               . VOp             ::= "<=" ;

SValue              . Value           ::= String  ;
IValue              . Value           ::= Integer ;
DValue              . Value           ::= Double ;

-- JSON Path

SinglePath            . Path            ::= "$" ;
MultiPath             . Path            ::= "$" LevelList ;

SingleLevelList       . LevelList       ::= Level ;
MultiLevelList        . LevelList       ::= Level LevelList ;

DottedLevel           . Level           ::= DottedAccessor Item ;
BracketLevel          . Level           ::= BracketAccessor ;

SelfDottedAccessor    . DottedAccessor  ::= "." ;
RecDottedAccessor     . DottedAccessor  ::= ".." ;

AllItem               . Item            ::= "*" ;
SingleItem            . Item            ::= Member ;
BracketItem           . Item            ::= BracketAccessor ;

SingleBracketAccessor . BracketAccessor ::= "[" Subscript "]" ;

AllSubscript          . Subscript       ::= "*" ;
IntegerSubscript      . Subscript       ::= Integer ;
MemberSubscript       . Subscript       ::= "'" Member "'" ;

token Member letter (letter | digit | '_' )* ;
